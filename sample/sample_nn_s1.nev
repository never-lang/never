
func randomize(seed : int) -> () -> int
{
    let v = seed;
    func rand() -> int
    {
        v = (v * 1103515245 + 12345) % 2147483648
    };
    rand
}

func sigmoid(x : float) -> float
{
    1.0 / (1.0 + exp(-x))
}

func one_matrix(W[D1, D2] : float) -> int
{
    let r = 0;
    let c = 0;
    
    for (r = 0; r < D1; r = r + 1)
    {
        for (c = 0; c < D2; c = c + 1)
        {
            W[r, c] = 1.0
        }
    }
}

func rand_matrix(W[D1, D2] : float, rand() -> int) -> int
{
    let r = 0;
    let c = 0;
    
    for (r = 0; r < D1; r = r + 1)
    {
        for (c = 0; c < D2; c = c + 1)
        {
            W[r, c] = (rand() % 1000) / 1000.0
        }
    }
}

func sigmoid_matrix(W[D1, D2] : float) -> [_,_] : float
{
    let r = 0;
    let c = 0;
    let S = {[ D1, D2 ]} : float;
    
    for (r = 0; r < D1; r = r + 1)
    {
        for (c = 0; c < D2; c = c + 1)
        {
            S[r, c] = sigmoid(W[r, c])
        }
    };
    S
}

func T_matrix(W[D1, D2] : float) -> [_,_] : float
{
    let r = 0;
    let c = 0;
    let T = {[ D2, D1 ]} : float;
    
    for (r = 0; r < D1; r = r + 1)
    {
        for (c = 0; c < D2; c = c + 1)
        {
            T[c, r] = W[r, c]
        }
    };
    T
}

func Hadamard_matrix(W1[D1, D2] : float, W2[D3, D4] : float) -> [_,_] : float
{
    let r = 0;
    let c = 0;
    let H = {[ D1, D2 ]} : float;
    
    for (r = 0; r < D1; r = r + 1)
    {
        for (c = 0; c < D2; c = c + 1)
        {
            H[r, c] = W1[r, c] * W2[r, c]
        }
    };
    H
}

func print_matrix(W[D1, D2] : float) -> int
{
    let r = 0;
    let c = 0;
    
    for (r = 0; r < D1; r = r + 1)
    {
        for (c = 0; c < D2; c = c + 1)
        {
            prints(W[r, c] + " ")
        };
        prints("\n")
    }
}

func nn() -> int
{
    let y = [ [1, 0, 1, 0] ] : float;
    let yT = T_matrix(y);
    let W = {[ 3, 1 ]} : float;
    let z = {[ 4, 1 ]} : float;
    let s = {[ 4, 1 ]} : float;
    let sD = {[ 4, 1 ]} : float;
    let err = {[ 4, 1 ]} : float;
    let one = {[ 4, 1 ]} : float;
    let rand = randomize(165);

    one_matrix(one);
    rand_matrix(W, rand);

    let i = 0;

    for (i = 0; i < 1000; i = i + 1)
    {
        let x = [ [0, 1, 0],
                [1, 0, 0],
                [1, 1, 1],
                [0, 0, 1] ] : float;
        let xT = T_matrix(x);

        z = x * W;
        s = sigmoid_matrix(z);

        err = yT - s;
        sD = Hadamard_matrix(s, one - s);
        W = W + xT * Hadamard_matrix(err, sD)
    };
    
    z = ([[ 1, 1, 1 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);

    z = ([[ 1, 1, 0 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);

    z = ([[ 1, 0, 1 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);

    z = ([[ 1, 0, 0 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);

    z = ([[ 0, 1, 1 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);

    z = ([[ 0, 1, 0 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);

    z = ([[ 0, 0, 1 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);

    z = ([[ 0, 0, 0 ]] : float) * W;
    s = sigmoid_matrix(z);
    print_matrix(s);
    
    0   
}

func main() -> int
{
    nn()
}

