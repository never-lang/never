
record tab
{
    t[T] : int;
}

func filter(t : tab, f(e : int) -> bool) -> tab
{
    tab( [ e | e in t.t; f(e) == true ] : int )
}

func map(t : tab, f(e : int) -> int) -> tab
{
    tab( [ f(e) | e in t.t ] : int )
}

func reduce(t : tab, i : int, f(a : int, e : int) -> int) -> int
{
    let a = i;
    let j = 0;

    for (j = 0; j < t.T; j = j + 1)
    {
        a = f(a, t.t[j])
    };

    a
}

func print_tab(t : tab) -> int
{
    let i = 0;

    for (i = 0; i < t.T; i = i + 1)
    {
        print(t.t[i])
    };

    0
}

func main() -> int
{
    let t = tab( [1, 2, 3, 4, 5, 6, 7, 8] : int );
    let s = tab;

    prints("---filter.map.reduce--1---\n");
    s = filter(t, let func(e : int) -> bool { (e % 2) == 0 });
    s = map(s, let func(e : int) -> int { 10 * e });
    print( reduce(s, 0, let func(a : int, e : int) -> int { a + e }) );

    prints("---filter.map.reduce--2---\n");
    print(reduce(map(filter(t,
                            let func(e : int) -> bool { (e % 2) == 0 }),
                     let func(e : int) -> int { 10 * e }),
                 0,
                 let func(a : int, e : int) -> int { a + e })
         );
    
    prints("---filter.map.reduce--3---\n");
    print(  t |> filter(let func(e : int) -> bool { (e % 2) == 0 })
              |> map(let func(e : int) -> int { 10 * e })
              |> reduce(0, let func(a : int, e : int) -> int { a + e })
         );

    0
}
